apply plugin: 'com.android.application'

// apply plugin: 'kotlin-android'
// apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion rootProject.compileSdkVersion
    defaultConfig {
        applicationId "com.landenlabs.all_uitest"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 202
        versionName '2.02'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Limit languages to following list.
        resConfigs "en", "es"

        ndk {
            // abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
    }

    // Save app version in string so resource can be used in layouts
    applicationVariants.all { variant ->
        variant.resValue "string", "versionName", "version " + variant.versionName
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
        disable 'DefaultLocale', 'SimpleDateFormat', 'InflateParams', 'SpUsage',
                'UseCompoundDrawables', 'HandlerLeak', 'ViewConstructor', 'SetTextI18n',
                'RtlSymmetry', 'RtlHardcoded', 'ContentDescription',
                'MissingTranslation', 'ValidFragment', 'UnusedAttribute',
                'FieldCanBeLocal', 'LocalSuppress', 'HardcodedText', 'Typos'
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            // Enable verbose lint reporting during build.
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }

    productFlavors {
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
//    implementation fileTree(dir: 'libs', include: ['*.jar'])
//    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$rootProject.kotlinVersion"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
//    implementation "android.arch.navigation:navigation-fragment-ktx:$rootProject.kotlinNavigationVersion"
//    implementation "android.arch.navigation:navigation-ui-ktx:$rootProject.kotlinNavigationVersion"

    implementation 'androidx.navigation:navigation-fragment:2.2.0-rc04'
    implementation 'androidx.navigation:navigation-ui:2.2.0-rc04'

    testImplementation "junit:junit:$rootProject.junitVersion"
    androidTestImplementation "androidx.test:runner:$rootProject.runnerVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.espressoVersion"
}

// Rename app
android.applicationVariants.all { variant ->
    variant.outputs.all {
        def ourname = project.parent.name
        outputFileName = "$ourname-${variant.name}-${variant.versionName}.apk"
        println "out name= $outputFileName"
    }
}
